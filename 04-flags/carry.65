; header
.byte $4e, $45, $53, $1a, 01, 00, 00, 00, 00, 00, 00, 00, 00, 00, 00, 00

* = $8000

chrdata:
.byte 0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0

; checkmark
.byte 0, 0, 0, $38, $1c, $0e, 3, 1, 0,0,0,0,0,0,0,0 
.byte 0, $0e, $1c, $38, $70, $e0, $c0, $80, 0,0,0,0,0,0,0,0

; x mark
.byte 0,0,0,0,0,0,0,0, 0, $18, $0c, $06, 1, $0e, $1c, $38
.byte 0,0,0,0,0,0,0,0, $0e, $38, $70, $e0, $80, $e0, $70, 0

; end marker
.byte $ff

palette:
.byte 13, $1a, $07, $3c, 0,0,0,0, 0,0,0,0, 0,0,0,0

int_reset:
    ; Load pattern table
    
    lda #0
    sta $2006
    sta $2006
    ldx #0

pattern_table:
    lda chrdata, x
    sta $2007
    inx
    cmp #$ff
    bne pattern_table


    ; Load palettes
    lda #$3f
    sta $2006
    lda #$0
    sta $2006

    ldx #$0

palette_table:
    lda palette, x
    sta $2007
    inx
    cpx #$10
    bne palette_table
    
    ; logic test here
    sec
    bcc fail
    clc
    bcs fail

    lda #$f0 
    bcs fail 
    adc #$f
    bcs fail
    adc #1
    bcc fail
    adc #1
    bcs fail

    clc     ; subtract operations with negative result doesn't turn on carry flag
    lda #0
    bcs fail
    sbc #1
    bcs fail

    clc     ; carry flag is on when A >= value in sbc
    lda #2
    bcs fail
    sbc #1
    bcc fail

    clc     ; sbc and cmp have the same carry flag behavior
    lda #1
    cmp #1
    bcc fail

    clc
    lda 0
    cmp #1
    bcs fail

    ; carry flag is set to the rightmost bit before shifting
    ; so any odd number shifted right will turn on the carry flag
    clc         
    lda #5    
    sta $1000
    bcs fail
    lsr $1000
    bcc fail
    lsr $1000
    bcs fail
    ror $1000
    bcc fail

    clc
    lda #$a0
    sta $1030
    bcs fail
    asl $1030
    bcc fail
    asl $1030
    bcs fail
    rol $1030
    bcc fail

ok:
    lda #$20
    sta $2006
    lda #$30
    sta $2006
    lda #1
    sta $2007
    lda #2
    sta $2007
    jmp done

fail:
    lda #$20
    sta $2006
    lda #$30
    sta $2006
    lda #3
    sta $2007
    lda #4
    sta $2007
    jmp done

done:
    ; reset PPU 
    lda #0
    sta $2006
    sta $2006
        
    ; enable PPU
    lda #%10001000
    sta $2000
    lda #%00011110
    sta $2001
	
; done, just wait around
main:	jmp main


int_vblank:
int_irqbrk:
	rti

; fill the rest of this bank with zeroes
.dsb $bffa-*, 0

; interrupt vectors
.word int_vblank, int_reset, int_irqbrk